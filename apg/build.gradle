apply plugin: 'android'

dependencies {
    compile 'com.android.support:support-v4:19.0.1'
    compile 'com.android.support:appcompat-v7:19.0.1'
    compile 'com.github.johnkil.android-appmsg:appmsg:1.2.0'
    compile 'se.emilsjolander:stickylistheaders:2.1.5'
    compile 'com.google.zxing:core:2.2'
    compile 'com.google.zxing:android-integration:3.0.0'
    compile project(':libs:AndroidBootstrap:AndroidBootstrap')
    compile project(':libs:bc:core')
    compile project(':libs:bc:pg')
    compile project(':libs:bc:prov')
    compile project(':libs:HtmlTextView')
    compile project(':OpenPGP-Keychain-API:libraries:openpgp-api-library')
    compile project(':OpenPGP-Keychain-API:libraries:openkeychain-api-library')

    compile project(':libs:k-9:plugins:Android-PullToRefresh:library')
    compile project(':libs:k-9:plugins:ckChangeLog:library')
    compile project(':libs:k-9:plugins:HoloColorPicker')
    compile fileTree(dir: '../libs/k-9/libs', include: '*.jar')
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.3"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 19
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    buildTypes {
        release {
            zipAlign true
            runProguard true
            proguardFile file('../proguard.cfg')
            proguardFile getDefaultProguardFile('proguard-android.txt')
        }
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java',
                            '../libs/k-9/src',
                          ]
            res.srcDirs = ['src/main/res',
                           '../libs/k-9/res',
                          ]
        }
    }

    if (project.hasProperty('signingStoreLocation') &&
        project.hasProperty('signingStorePassword') &&
        project.hasProperty('signingKeyAlias') &&
        project.hasProperty('signingKeyPassword')) {

        signingConfigs {
            release {
                storeFile file(signingStoreLocation)
                storePassword signingStorePassword
                keyAlias signingKeyAlias
                keyPassword signingKeyPassword
            }
        }
        buildTypes.release.signingConfig = signingConfigs.release
    } else {
        buildTypes.release.signingConfig = null
    }

    // Do not abort build if lint finds errors
    lintOptions {
        abortOnError false
    }
}
